
apply plugin: 'java'
def fineVersion = '10.0-RELEASE-SNAPSHOT'

ext {
    /**
     * 项目中依赖的jar的路径
     * 1.如果依赖的jar需要打包到zip中,放置在lib根目录下
     * 2.如果依赖的jar仅仅是编译时需要，防止在lib下子目录下即可
     */
    libPath = "$projectDir/../webroot/WEB-INF/lib"

    /**
     * 是否对插件的class进行加密保护，防止反编译
     */
    guard = false

    def pluginInfo = getPluginInfo()
    pluginPre = "fine-plugin"
    pluginName = pluginInfo.id
    pluginVersion = pluginInfo.version

    outputPath = "$projectDir/../webroot/WEB-INF/plugins/plugin-" + pluginName + "-1.0/classes"
}

group = 'com.fr.plugin'
version = '10.0'
sourceCompatibility = '8'

sourceSets {
    main {
        java.outputDir = file(outputPath)
        output.resourcesDir = file(outputPath)
    }
}

ant.importBuild("encrypt.xml")
//定义ant变量
ant.projectDir = projectDir
ant.references["compile.classpath"] = ant.path {
    fileset(dir: libPath, includes: '**/*.jar')
    fileset(dir: ".",includes:"**/*.jar" )
}

classes.dependsOn('clean')

task copyFiles(type: Copy,dependsOn: 'classes'){
    from outputPath
    into "$projectDir/classes"
}

task preJar(type:Copy,dependsOn: guard ? 'compile_encrypt_javas' : 'compile_plain_javas'){
    from "$projectDir/classes"
    into "$projectDir/transform-classes"
    include "**/*.*"
}
jar.dependsOn("preJar")

task makeJar(type: Jar,dependsOn: preJar){
    from fileTree(dir: "$projectDir/transform-classes")
    baseName pluginPre
    appendix pluginName
    version pluginVersion
    destinationDir = file("$buildDir/libs")

    doLast(){
        delete file("$projectDir/classes")
        delete file("$projectDir/transform-classes")
    }
}

task copyFile(type: Copy,dependsOn: ["makeJar"]){
    from "$buildDir/libs"
    from("$projectDir/lib") {
        include "*.jar"
    }
    from "$projectDir/plugin.xml"
    into file("$buildDir/temp/plugin")
}

task zip(type:Zip,dependsOn:["copyFile"]){
    from "$buildDir/temp/plugin"
    destinationDir file("$buildDir/install")
    baseName pluginPre
    appendix pluginName
    version pluginVersion
}

//控制build时包含哪些文件,排除哪些文件
processResources {
//    exclude everything
//    用*.css没效果
//    exclude '**/*.css'
//    except this file
//    include 'xx.xml'
}

/*读取plugin.xml中的version*/
def getPluginInfo(){
    def xmlFile = file("plugin.xml")
    if (!xmlFile.exists()) {
        return ["id":"none", "version":"1.0.0"]
    }
    def plugin = new XmlParser().parse(xmlFile)
    def version = plugin.version[0].text()
    def id = plugin.id[0].text()
    return ["id":id,"version":version]
}

repositories {
    mavenLocal()
    maven {
        url = uri('http://mvn.finedevelop.com/repository/maven-public/')
        allowInsecureProtocol = true
    }
}


allprojects {

    apply plugin: 'java'

    dependencies {
        implementation 'com.fr.third:fine-third:' + fineVersion
        implementation 'com.fr.activator:fine-activator:' + fineVersion
        implementation 'com.fr.core:fine-core:' + fineVersion
        implementation 'com.fr.webui:fine-webui:' + fineVersion
        implementation 'com.fr.datasource:fine-datasource:' + fineVersion
        implementation 'com.fr.decision:fine-decision:' + fineVersion
        implementation 'com.fr.decision:fine-decision-report:' + fineVersion
        implementation 'com.fr.schedule:fine-schedule:' + fineVersion
        implementation 'com.fr.schedule:fine-schedule-report:' + fineVersion
        implementation 'com.fr.intelligence:fine-swift:' + fineVersion
        implementation 'com.fr.intelligence:fine-accumulator:' + fineVersion
        implementation 'com.fr.report:fine-report-engine:' + fineVersion
        implementation 'com.fr.report:fine-report-designer:' + fineVersion
        implementation 'io.socket:socket.io-client:0.7.0'
        implementation 'org.aspectj:aspectjrt:1.6.9'
        implementation 'org.swingexplorer:swexpl:2.0'
        implementation 'org.swingexplorer:swag:1.0'
        implementation 'org.apache.tomcat:tomcat-catalina:8.5.32'
        implementation 'org.apache.tomcat:tomcat-jasper:8.5.32'
        implementation 'mysql:mysql-connector-java:5.1.44'
        implementation 'com.fr.third:jxbrowser:6.23.1'
        implementation group: "com.fr.third", name: "jxbrowser-win64", version: "6.23.1"
        implementation 'org.slf4j:slf4j-api:1.7.30'
        implementation 'org.slf4j:slf4j-simple:1.7.30'
        testImplementation 'junit:junit:4.12'
        testImplementation 'org.easymock:easymock:3.5.1'
    }

}
dependencies {
    //使用本地jar
    implementation  fileTree(dir: 'lib', include: ['**/*.jar'])
    implementation  fileTree(dir: libPath, include: ['**/*.jar'])
}

